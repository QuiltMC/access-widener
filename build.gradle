plugins {
	id "java"
	id "java-library"
	id "checkstyle"
	id "maven-publish"
	// TODO: Figure out signing
	//id "me.modmuss50.remotesign" version "0.1.0"
	id "com.diffplug.spotless" version "5.8.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group "org.quiltmc"
archivesBaseName = "access-widener"

def ENV = System.getenv()
version = "1.0.2"
logger.lifecycle("Building access-widener: " + version)

repositories {
	mavenCentral()

	maven {
		name = "Quilt"
		url = "https://maven.quiltmc.org/repository/release"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

dependencies {
	def asmVersion = "9.0"

	api "org.ow2.asm:asm:$asmVersion"
	api "org.ow2.asm:asm-commons:$asmVersion" // For Remapper
}

spotless {
	java {
		// Do not update headers unless
		ratchetFrom "origin/master"

		// Use comma separator for openjdk like license headers
		licenseHeaderFile(project.file("HEADER")).yearSeparator(", ")
	}
}

checkstyle {
	configFile = project.file("checkstyle.xml")
	toolVersion = "8.31"
}

java {
	withSourcesJar()
	withJavadocJar()
}

//if (ENV.SIGNING_SERVER) {
//	remoteSign {
//		requestUrl ENV.SIGNING_SERVER
//		pgpAuthKey ENV.SIGNING_PGP_KEY
//		jarAuthKey ENV.SIGNING_JAR_KEY
//
//		sign (jar, javadocJar, sourcesJar)
//
//		afterEvaluate {
//			sign publishing.publications.mavenJava
//		}
//	}
//}

publishing {
	publications {
		mavenJava(MavenPublication) {
//			if (ENV.SIGNING_SERVER) {
//				artifact(signJar) {
//					classifier null
//				}
//
//				artifact(signJavadocJar) {
//					classifier "javadoc"
//				}
//
//				artifact(signSourcesJar) {
//					classifier "sources"
//				}
//			} else {
				from components.java

				artifact(javadocJar)
//			}

			pom {
				name = 'access-widener'
				description = 'Access widener provides a way to loosen the access limits of classes in a data driven manner.'
				url = 'https://github.com/QuiltMC/access-widener'

				scm {
					connection = "scm:git:https://github.com/QuiltMC/access-widener.git"
					developerConnection = "scm:git:git@github.com:QuiltMC/access-widener.git"
					url = "https://github.com/QuiltMC/access-widener"
				}

				issueManagement {
					system = "GitHub"
					url = "https://github.com/QuiltMC/access-widener/issues"
				}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = "i509VCB"
						name = "i509VCB"
						email = "git@i509.me"
					}
				}
			}
		}
	}

	repositories {
		mavenLocal()

		if (ENV.MAVEN_URL) {
			repositories.maven {
				name "quilt"
				url ENV.MAVEN_URL

				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}

//		if (ENV.MAVEN_CENTRAL_URL) {
//			repositories.maven {
//				name "central"
//				url ENV.MAVEN_CENTRAL_URL
//				credentials {
//					username ENV.MAVEN_CENTRAL_USERNAME
//					password ENV.MAVEN_CENTRAL_PASSWORD
//				}
//			}
//		}
	}
}
